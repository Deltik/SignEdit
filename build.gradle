/*
 * Copyright (C) 2017-2025 Deltik <https://www.deltik.net/>
 *
 * This file is part of SignEdit for Bukkit.
 *
 * SignEdit for Bukkit is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * SignEdit for Bukkit is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with SignEdit for Bukkit.  If not, see <https://www.gnu.org/licenses/>.
 */

buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'java-library'
    id 'application'
    id 'com.palantir.git-version' version '0.12.3'
}

def name = 'SignEdit'

def verDetails = versionDetails()
def versionWithoutLeadingV = gitVersion().replaceFirst(/^v/, "")
if (verDetails.isCleanTag) {
    version versionWithoutLeadingV
} else {
    version versionWithoutLeadingV + '-SNAPSHOT'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }
}

repositories {
    mavenCentral()
    maven {
        url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
}

configurations {
    extraLibs
}

application {
    mainClass.set("net.deltik.mc.signedit.SignEditPlugin")
}

dependencies {
    implementation group: 'org.bukkit', name: 'bukkit', version: '1.13-R0.1-SNAPSHOT'
//    implementation group: 'org.spigotmc', name: 'spigot-api', version: '1.20.1-R0.1-SNAPSHOT'
    implementation 'com.intellij:annotations:12.0@jar'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.7.2'
    testImplementation group: 'org.junit.vintage', name: 'junit-vintage-engine', version: '5.7.2'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.10.0'
}

sourceSets {
    processor {
        java {
            srcDirs = ['src']
            include 'net/deltik/mc/signedit/subcommands/SignSubcommandProcessor.java'
            include 'net/deltik/mc/signedit/subcommands/SignSubcommandInfo.java'
        }
        resources {
            srcDirs = ['processor-resources']
        }
    }

    main {
        java {
            srcDirs = ['src']
        }
        resources {
            srcDirs = ['src/resources']
        }
    }

    test {
        java {
            srcDir 'test'
        }
        resources {
            srcDirs = ['test/resources']
        }
    }
}

compileProcessorJava {
    options.compilerArgs += ['-proc:none']
}

compileJava {
    dependsOn processProcessorResources
    options.annotationProcessorPath = files(
            sourceSets.processor.output.classesDirs,
            sourceSets.processor.output.resourcesDir
    )
}

compileTestJava {
    dependsOn compileJava
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = false
        afterSuite { desc, result ->
            if (!desc.parent) {
                println "\nTest results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
            }
        }
    }
}

jar {
    from {
        configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

processResources {
    def expandProperties = [
            pluginName   : name,
            pluginVersion: version,
            mainClass    : application.mainClass,
    ]
    inputs.properties(expandProperties)
    with copySpec {
        from 'src/resources'
        expand(expandProperties)
        duplicatesStrategy(DuplicatesStrategy.INCLUDE)
    }
}