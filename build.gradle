/*
 * Copyright (C) 2017-2023 Deltik <https://www.deltik.net/>
 *
 * This file is part of SignEdit for Bukkit.
 *
 * SignEdit for Bukkit is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * SignEdit for Bukkit is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with SignEdit for Bukkit.  If not, see <https://www.gnu.org/licenses/>.
 */

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath group: 'com.guardsquare', name: 'proguard-gradle', version: '7.2.0-beta2'
    }
}

plugins {
    id 'java'
    id 'java-library'
    id 'application'
    id 'com.palantir.git-version' version '0.12.3'
}

def name = 'SignEdit'

def verDetails = versionDetails()
def versionWithoutLeadingV = gitVersion().replaceFirst(/^v/, "")
if (verDetails.isCleanTag) {
    version versionWithoutLeadingV
} else {
    version versionWithoutLeadingV + '-SNAPSHOT'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }
}

repositories {
    mavenCentral()
    maven {
        url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
}

configurations {
    extraLibs
}

application {
    mainClass.set("net.deltik.mc.signedit.SignEditPlugin")
}

dependencies {
    implementation group: 'org.bukkit', name: 'bukkit', version: '1.13-R0.1-SNAPSHOT'
//    implementation group: 'org.spigotmc', name: 'spigot-api', version: '1.20.1-R0.1-SNAPSHOT'
    implementation 'com.intellij:annotations:12.0@jar'

    implementation group: 'com.google.dagger', name: 'dagger', version: '2.36'
    extraLibs group: 'com.google.dagger', name: 'dagger', version: '2.36'
    annotationProcessor group: 'com.google.dagger', name: 'dagger-compiler', version: '2.36'
    extraLibs group: 'javax.inject', name: 'javax.inject', version: '1'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.7.2'
    testImplementation group: 'org.junit.vintage', name: 'junit-vintage-engine', version: '5.7.2'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.10.0'
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
        resources {
            srcDirs = ['src/resources']
        }
    }

    test {
        java {
            srcDir 'test'
        }
        resources {
            srcDirs = ['test/resources']
        }
    }
}

jar {
    from {
        configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

processResources {
    def expandProperties = [
            pluginName   : name,
            pluginVersion: version,
            mainClass: application.mainClass,
    ]
    inputs.properties(expandProperties)
    with copySpec {
        from 'src/resources'
        expand(expandProperties)
        duplicatesStrategy(DuplicatesStrategy.INCLUDE)
    }
}

def proguardOutFile = rootProject.file("$buildDir/libs/release.jar")
def jarFile = jar.outputs.files.singleFile

//noinspection UnnecessaryQualifiedReference
tasks.register('proguard', proguard.gradle.ProGuardTask) {
    onlyIf { !project.version.endsWith('-SNAPSHOT') }

    configuration 'configuration.pro'

    injars jarFile
    outjars proguardOutFile
    libraryjars configurations.named('runtimeClasspath') {
        getFiles()
    }
    // Java 8
    libraryjars "${System.getProperty("java.home")}/lib/rt.jar"
    // Java 9+ compatibility (requires JDK)
    libraryjars "${System.getProperty("java.home")}/jmods/java.base.jmod"
    libraryjars "${System.getProperty("java.home")}/jmods/java.desktop.jmod"
    libraryjars "${System.getProperty("java.home")}/jmods/java.logging.jmod"
    libraryjars "${System.getProperty("java.home")}/jmods/jdk.unsupported.jmod"
}

jar.finalizedBy proguard
proguard.doLast {
    if (project.version.endsWith('-SNAPSHOT')) return
    if (!jarFile.delete()) throw new IllegalStateException()
    if (!proguardOutFile.renameTo(jarFile)) throw new IllegalStateException()
}